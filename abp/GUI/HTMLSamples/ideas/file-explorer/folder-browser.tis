
class FolderBrowser : Behavior
{
  function attached()
  {
    this.loadPath("C:");  
  }
  
  function goRoot()
  {
    this.loadPath("C:");  
  }
  
  function goUp()
  {
    var sp = this.path.split("\\");
    sp.pop();
	if(sp.length)
	  this.loadPath(sp.join("\\"));  
  }
  
  function onPathChanged(path) {}
  
  function loadPath(path)
  {
    this.path = path;
    this.clear();
    var me = this;
    function w( name, flags )
    {
      if(name == "." || name == "..") return true;
      if(flags & (System.IS_HIDDEN | System.IS_SYSTEM)) return true; // skip it
      var node = new Element( "option" );
      node.@#filename = path + "\\" + name;
      node.isFolder = (flags & System.IS_DIR) != 0;
      var node_text = new Element( "text", name); 
          node.insert(node_text);
      me.insert(node);
      return true;
    }
    System.scanFiles(path + "\\*.*", w);
    
    function comparator(node1,node2)
    {
      if( node1.isFolder && !node2.isFolder ) // folders go first
        return -1;
      if( !node1.isFolder && node2.isFolder ) // files go after
        return 1;
      return node1.@#filename < node2.@#filename? -1:1;
    }
    this.sort(comparator);
        
    this.onPathChanged(path);
  }
  
  function onMouse(evt)
  {
    if( evt.type != Event.MOUSE_DCLICK )
      return;
    var opt = evt.target.$p(option);
    if( opt.isFolder )
    {
      this.loadPath(opt.@#filename);
      return true;
    }
  }
  
}